// SPDX-License-Identifier: BUSL 1.1

pragma solidity ^0.8.24;

import "./INativeOrders.sol";

/// @dev Batch-reacd order info
contract OrderLens {
    error mismatchedArrayLengths();

    address immutable ORDER_CONTRACT;
    
    constructor(address orderContract) {
        ORDER_CONTRACT = orderContract;
    }

    /// @dev Batch version of `getLimitOrderRelevantState()`, without reverting.
    ///      Orders that would normally cause `getLimitOrderRelevantState()`
    ///      to revert will have empty results.
    /// @param orders The limit orders.
    /// @param signatures The order signatures.
    /// @return orderInfos Info about the orders.
    /// @return actualFillableTakerTokenAmounts How much of each order is fillable
    ///         based on maker funds, in taker tokens.
    /// @return isSignatureValids Whether each signature is valid for the order.
    function batchGetLimitOrderRelevantStates(
        LibNativeOrder.LimitOrder[] calldata orders,
        LibSignature.Signature[] calldata signatures
    )
        external
        view
        returns (
            LibNativeOrder.OrderInfo[] memory orderInfos,
            uint128[] memory actualFillableTakerTokenAmounts,
            bool[] memory isSignatureValids
        )
    {
        if(orders.length != signatures.length) revert mismatchedArrayLengths();
        orderInfos = new LibNativeOrder.OrderInfo[](orders.length);
        actualFillableTakerTokenAmounts = new uint128[](orders.length);
        isSignatureValids = new bool[](orders.length);
        for (uint256 i; i != orders.length; ++i) {
            try INativeOrders(ORDER_CONTRACT).getLimitOrderRelevantState(orders[i], signatures[i]) returns (
                LibNativeOrder.OrderInfo memory orderInfo,
                uint128 actualFillableTakerTokenAmount,
                bool isSignatureValid
            ) {
                orderInfos[i] = orderInfo;
                actualFillableTakerTokenAmounts[i] = actualFillableTakerTokenAmount;
                isSignatureValids[i] = isSignatureValid;
            } catch {}
        }
    }

    /// @dev Batch version of `getRfqOrderRelevantState()`, without reverting.
    ///      Orders that would normally cause `getRfqOrderRelevantState()`
    ///      to revert will have empty results.
    /// @param orders The RFQ orders.
    /// @param signatures The order signatures.
    /// @return orderInfos Info about the orders.
    /// @return actualFillableTakerTokenAmounts How much of each order is fillable
    ///         based on maker funds, in taker tokens.
    /// @return isSignatureValids Whether each signature is valid for the order.
    function batchGetRfqOrderRelevantStates(
        LibNativeOrder.RfqOrder[] calldata orders,
        LibSignature.Signature[] calldata signatures
    )
        external
        view
        returns (
            LibNativeOrder.OrderInfo[] memory orderInfos,
            uint128[] memory actualFillableTakerTokenAmounts,
            bool[] memory isSignatureValids
        )
    {
        if(orders.length != signatures.length) revert mismatchedArrayLengths();
        orderInfos = new LibNativeOrder.OrderInfo[](orders.length);
        actualFillableTakerTokenAmounts = new uint128[](orders.length);
        isSignatureValids = new bool[](orders.length);
        for (uint256 i; i != orders.length; ++i) {
            try INativeOrders(ORDER_CONTRACT).getRfqOrderRelevantState(orders[i], signatures[i]) returns (
                LibNativeOrder.OrderInfo memory orderInfo,
                uint128 actualFillableTakerTokenAmount,
                bool isSignatureValid
            ) {
                orderInfos[i] = orderInfo;
                actualFillableTakerTokenAmounts[i] = actualFillableTakerTokenAmount;
                isSignatureValids[i] = isSignatureValid;
            } catch {}
        }
    }
}
